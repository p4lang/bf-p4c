# Maintainer: Jed Liu <jliu@barefootnetworks.com>
pkgname=p4-pi-git
pkgver=r0
pkgrel=1
pkgdesc="P4 runtime: control plane framework and tools for P4"
arch=('x86_64')
url="https://github.com/p4lang/PI"
license=('custom')
depends=('grpc' 'judy' 'protobuf')
optdepends=('valgrind')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/p4lang/PI')
md5sums=('SKIP')

prepare() {
  cd "${srcdir}/PI"
  git submodule update --init --recursive
}

pkgver() {
  cd "${srcdir}/PI"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/PI"
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin \
    --with-proto --without-internal-rpc --without-cli
  make
}

package() {
  cd "${srcdir}/PI"
  make DESTDIR="${pkgdir}/" install
}
