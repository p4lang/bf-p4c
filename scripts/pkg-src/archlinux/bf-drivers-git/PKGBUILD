# Maintainer: Jed Liu <jliu@barefootnetworks.com>
pkgname=bf-drivers-git
pkgver=r0
pkgrel=1
pkgdesc="BFN: Core drivers"
arch=('x86_64')
url="https://github.com/barefootnetworks/bf-drivers"
license=('custom')
depends=('bf-syslibs' 'bf-utils' 'p4-pi')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")

_gitbranch='brig-stable'
_giturl='git@github.com:barefootnetworks/bf-drivers'

prepare() {
  cd "${srcdir}"
  git clone --recursive -b "${_gitbranch}" "${_giturl}" "${pkgname%-git}"
}

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  export CFLAGS+=" -Wno-error"
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin \
    --disable-thrift --with-avago --without-kdrv --with-build-model --enable-pi
  make
}

package() {
  cd "${srcdir}/${pkgname%-git}"
  make DESTDIR="${pkgdir}/" install
}
