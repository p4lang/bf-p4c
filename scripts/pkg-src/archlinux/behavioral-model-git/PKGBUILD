# Maintainer: Jed Liu <jliu@barefootnetworks.com>
pkgname=behavioral-model-git
pkgver=r0
pkgrel=1
pkgdesc="P4 behavioral model"
arch=('x86_64')
url="https://github.com/p4lang/behavioral-model"
license=('Apache')
depends=('boost' 'judy' 'libevent' 'nanomsg' 'thrift' 'python2-thrift')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/p4lang/behavioral-model')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
  ./targets/simple_switch/simple_switch -h &>/dev/null
}

package() {
  cd "${srcdir}/${pkgname%-git}"
  make DESTDIR="${pkgdir}/" install
}
