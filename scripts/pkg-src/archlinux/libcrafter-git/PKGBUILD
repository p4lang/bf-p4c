# Maintainer: Jed Liu <jliu@barefootnetworks.com>
pkgname=libcrafter-git
pkgver=r0
pkgrel=1
pkgdesc="A high-level C++ library for sniffing and crafting network packets"
arch=('x86_64')
url="https://github.com/pellegre/libcrafter"
license=('BSD')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/pellegre/libcrafter')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname%-git}/${pkgname%-git}"
  ./autogen.sh
  ./configure --prefix=/usr --sbin=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname%-git}/${pkgname%-git}"
  make DESTDIR="${pkgdir}/" install
}
