# Maintainer: Jed Liu <jliu@barefootnetworks.com>
pkgname=bf-utils-git
pkgver=r0
pkgrel=1
pkgdesc="BFN: utility modules"
arch=('x86_64')
url="https://github.com/barefootnetworks/bf-utils"
license=('custom')
depends=('bf-syslibs')
makedepends=('libffi')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('staticlibs') # bf-drivers requires static libraries

_gitbranch='brig-stable'
_giturl='git@github.com:barefootnetworks/bf-utils'

prepare() {
  cd "${srcdir}"
  git clone --recursive -b "${_gitbranch}" "${_giturl}" "${pkgname%-git}"
}

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname%-git}"
  make DESTDIR="${pkgdir}/" install

  # Remove files that conflict with libffi.
  for f in $(pacman -Ql libffi | grep -v '/$' | sed 's/^libffi //') ; do
    rm -f "${pkgdir}/$f"

    # Also remove uncompressed manpages.
    if echo "$f" | grep '^/usr/share/man' &>/dev/null ; then
      rm -f "${pkgdir}/$(echo "$f" | sed 's/\.gz$//')"
    fi
  done
}
