# Maintainer: Jed Liu <jliu@barefootnetworks.com>
pkgname=python2-ptf-git
pkgver=r0
pkgrel=1
pkgdesc="P4 PTF Packet Testing Framework"
arch=('x86_64')
url="https://github.com/p4lang/ptf"
license=('Apache')
depends=('python2' 'scapy')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install=python2-ptf-git.install
source=('git+https://github.com/p4lang/ptf' '50-ptf.conf')
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/ptf"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "${srcdir}/ptf"
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -m 0644 -D ../50-ptf.conf "$pkgdir"/etc/sysctl.d/50-ptf.conf
  install -m 0755 -d "$pkgdir"/mnt/huge
}
